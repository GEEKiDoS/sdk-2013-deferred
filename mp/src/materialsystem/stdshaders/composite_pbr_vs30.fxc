//==================================================================================================
//
// Physically Based Rendering vertex shader for brushes and models
//
//==================================================================================================

//  STATIC: "LIGHTMAPPED"               "0..1"
//  DYNAMIC: "COMPRESSED_VERTS"         "0..1"
//  DYNAMIC: "DOWATERFOG"               "0..1"
//  DYNAMIC: "SKINNING"                 "0..1"
//  DYNAMIC: "LIGHTING_PREVIEW"         "0..1"
//	DYNAMIC: "STATIC_LIGHT_VERTEX"		"0..1"

#include "common_vs_fxc.h"

static const bool g_bSkinning           = SKINNING ? true : false;
static const int g_FogType              = DOWATERFOG;
const float4 cBaseTexCoordTransform[2]  : register(SHADER_SPECIFIC_CONST_0);

//-----------------------------------------------------------------------------
// Input vertex format
//-----------------------------------------------------------------------------
struct VS_INPUT
{
    // This is all of the stuff that we ever use
    float4 vPos                     : POSITION;
    float4 vBoneWeights             : BLENDWEIGHT;
    float4 vBoneIndices             : BLENDINDICES;
    float4 vNormal                  : NORMAL;
	float4 vColor                   : COLOR0;
	float3 vSpecular                : COLOR1;
    float2 vTexCoord0               : TEXCOORD0;
#ifdef LIGHTMAPPED
    float4 vLightmapTexCoord        : TEXCOORD1;
    float4 vLightmapTexCoordOffset  : TEXCOORD2;
#endif
};

struct VS_OUTPUT
{
    // Stuff that isn't seen by the pixel shader
    float4 projPosSetup             : POSITION;
    float  fog                      : FOG;
    // Stuff that is seen by the pixel shader
    float2 baseTexCoord             : TEXCOORD0;
    float3 worldNormal              : TEXCOORD1;
    float3 worldPos                 : TEXCOORD2;
    float3 projPos                  : TEXCOORD3;
    float4 lightmapTexCoord0And1    : TEXCOORD4;
    float4 lightmapTexCoord2And3    : TEXCOORD5;
	float3 staticLighting : TEXCOORD6;
};

//-----------------------------------------------------------------------------
// Main shader entry point
//-----------------------------------------------------------------------------
VS_OUTPUT main( const VS_INPUT v )
{
    VS_OUTPUT o = ( VS_OUTPUT )0;

    float3 vNormal;
    DecompressVertex_Normal(v.vNormal, vNormal);

    float3 worldNormal, worldPos;
    SkinPositionAndNormal(g_bSkinning, v.vPos, vNormal, v.vBoneWeights, v.vBoneIndices, worldPos, worldNormal);

#if LIGHTMAPPED
    o.lightmapTexCoord0And1.xy = v.vLightmapTexCoord;
    o.lightmapTexCoord0And1.zw = o.lightmapTexCoord0And1.xy + v.vLightmapTexCoordOffset;
    o.lightmapTexCoord2And3.xy = o.lightmapTexCoord0And1.zw + v.vLightmapTexCoordOffset;
    o.lightmapTexCoord2And3.zw = o.lightmapTexCoord2And3.xy + v.vLightmapTexCoordOffset;
#elif STATIC_LIGHT_VERTEX
    o.staticLighting.rgb = GammaToLinear(v.vSpecular);
#endif
    
    // Transform into projection space
    float4 vProjPos = mul(float4(worldPos, 1), cViewProj);
    o.projPosSetup = vProjPos;
    vProjPos.z = dot(float4(worldPos, 1), cViewProjZ);

    o.projPos = vProjPos.xyz;
    o.fog = CalcFog(worldPos, vProjPos.xyz, g_FogType);

    // Needed for water fog alpha and diffuse lighting 
    o.worldPos = worldPos;
    o.worldNormal = normalize(worldNormal);
    
    // Base texture coordinate transform
    o.baseTexCoord.x = dot(v.vTexCoord0, cBaseTexCoordTransform[0].xy);
    o.baseTexCoord.y = dot(v.vTexCoord0, cBaseTexCoordTransform[1].xy);

    return o;
}
